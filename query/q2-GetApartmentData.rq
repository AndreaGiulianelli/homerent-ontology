PREFIX : <https://purl.org/homerent/>
PREFIX schema: <http://schema.org/>
PREFIX gr: <http://purl.org/goodrelations/v1#>

SELECT ?availableFrom ?readyToLive ?furnished ?minAvailabilityValue ?maxAvailabilityValue ?availabilityUnit ?builtIn ?lastRenovationYear ?floor ?rooms ?description
       ?floorType
       (CONCAT(?streetAddress, ", ", ?postalCode, ", ", ?locality, ", ", ?country) AS ?address)
WHERE {
    ?apartment :isAvailableFrom ?availableFrom ;
                :isReadyToLive ?readyToLive ;
                :isFurnished ?furnished .
    OPTIONAL {
        ?apartment :isAvailableFor ?availability .
        ?availability gr:hasUnitOfMeasurement ?availabilityUnit.
        OPTIONAL { ?availability gr:hasMinValueFloat ?minAvailabilityValue . }
        OPTIONAL { ?availability gr:hasMaxValueFloat ?maxAvailabilityValue . }
    }
    OPTIONAL { ?apartment schema:yearBuilt ?builtIn . }
    OPTIONAL { ?apartment :lastRenovationYear ?lastRenovationYear . }
    OPTIONAL { ?apartment schema:floorLevel ?floor . }
    OPTIONAL { ?apartment schema:numberOfRooms ?rooms . }
    OPTIONAL { ?apartment schema:description ?description . }
    OPTIONAL { ?apartment :hasFloorType ?floorType . }
    OPTIONAL {
        ?apartment schema:address ?apartmentAddress .
        ?apartmentAddress schema:addressCountry ?country ;
                 schema:addressLocality ?locality ;
                 schema:postalCode ?postalCode ;
                 schema:streetAddress ?streetAddress .
    }
    FILTER (?apartment = :Apartment1)
}
